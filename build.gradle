apply plugin: 'groovy'
apply plugin: 'application'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
    maven {
        url 'https://repo.jenkins-ci.org/public/'
    }
}

dependencies {
    implementation group: 'org.apache.groovy', name: 'groovy-all', version: '4.0.24'
    implementation 'org.slf4j:slf4j-api:2.0.16'
    implementation 'org.slf4j:slf4j-simple:2.0.16'
    implementation('org.jfrog.artifactory.client:artifactory-java-client-services:2.19.1') {
        exclude group: 'org.codehaus.groovy'
    }
    implementation fileTree('libs') {
        include '*.jar'
    }
    implementation 'net.praqma:praqmajutils:1.0.0'
    testImplementation group: 'org.spockframework', name: 'spock-core', version: '2.3-groovy-4.0'
}

task functionalTest(type: Test) {
    dependsOn('testClasses')
    include 'toGit/functional/**'
}

task uberjar(type: Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    dependsOn('compileGroovy')
    from files(sourceSets.main.output.classesDirs)
    from configurations.runtimeClasspath.asFileTree.files.collect { zipTree(it) }
    manifest {
        attributes 'Main-Class': 'toGit.Run'
    }
}

mainClassName='toGit.Run'

test {
    exclude 'toGit/functional/**'
}
